{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

(int) is_null(tuple t) inline asm "ISNULL";

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if (cell_hash(tree) == hash) {
    return tree;
  }

  tuple list = null();
  list = cons(tree, list);

  while (~ is_null(list)) {
    (cell c, list) = uncons(list);
    slice s = c.begin_parse();
  
    repeat (s.slice_refs()) {
      cell c1 = s~load_ref();
      if (cell_hash(c1) == hash) {
        return c1;
      }
      list = cons(c1, list);
    }
  }

  return begin_cell().end_cell();
}
