{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Flag and the value may be of different lengths.
  When there are overlapping flags, only the first one
  from the overlap needs to be replaced (for example, if the flag is 101,
  value is 111, and string is 10101, then the result would be 11101, but if
  the string is 1010101, then the result would be 1110111).
  Every cell in the input linked list, except for the last one
  (the one that is the deepest in the tree), is guaranteed to be full
  (guaranteed to contain 1023 bits).
  The requirements on the output cell are more loose - only the concatenation of bits
  from the linked list needs to match that of the expected answer, but the structure
  itself may be different (for example, for the purposes of comparing the answer,
  a cell with bit 1 and a ref containing bits 10 is the same as a cell containing
  bit 11 and a ref with bit 0 - they both make up a bitstring 110).
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}

(int) is_null(tuple t) inline asm "ISNULL";
(int) ubitsize(int x) inline asm "UBITSIZE";

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  tuple list = null();
  slice s = linked_list.begin_parse();
  builder b = begin_cell();

  int medium = 0;
  int medium_bits = 0;
  int flag_bits = ubitsize(flag);
  int value_bits = ubitsize(value);


  int flag = false;
  do {
    while (~ s.slice_data_empty?()) {
      int can_read = min(flag_bits - medium_bits, s.slice_bits());
      medium_bits += can_read;
      medium = (medium << can_read) + s~load_uint(can_read);

      if (medium_bits == flag_bits) {
        if (medium == flag) {
          if (b.builder_bits() + value_bits > 1023) {
            list = cons(b, list);
            b = begin_cell();
          }
          b~store_uint(value, value_bits);
          medium = 0;
          medium_bits = 0;
        } else {
          int left_bit = medium >> (medium_bits - 1);
          if (b.builder_bits() == 1023) {
            list = cons(b, list);
            b = begin_cell();
          }
          b~store_uint(left_bit, 1);
          medium_bits -= 1;
          medium = medium & ((1 << medium_bits) - 1);
        }
      }
    }

    if (s.slice_refs()) {
      s = s~load_ref().begin_parse();
    } else {
      flag = true;
    }
  } until (flag);

  if (medium_bits > 0) {
    if (b.builder_bits() + medium_bits > 1023) {
      list = cons(b, list);
      b = begin_cell();
    }
    b~store_uint(medium, medium_bits);
  }

  while (~ is_null(list)) {
    cell c = b.end_cell();
    (b, list) = uncons(list);
    b = b.store_ref(c);
  }
  return b.end_cell();
}

